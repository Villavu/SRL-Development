{$DEFINE SRL_OPTIONS_INCLUDED}
{$IFNDEF SRL_OSR}
  {$I SRL/osr.simba}
{$ENDIF}

type
  ERSOptionsTab = (
    CONTROLS,
    AUDIO,
    DISPLAY
  );

  ERSOptionsButton = (
    AID,
    RUN,
    HOUSE,
    BOND
  );

  ERSHouseOptionsButton = (
    CLOSE,
    VIEWER,
    EXPEL,
    SERVANT,
    LEAVE
  );

  TRSSlider = record
    Bounds: TBox;
    Color: TCTS0Color;
  end;

  TRSOptions = record(TRSInterface)
    BrightnessSlider: TRSSlider;
    ZoomSlider: TRSSlider;
  end;

procedure TRSOptions.Setup; override;
begin
  inherited;

  Self.Name := 'Options';
  Self.ButtonColors += CTS1(2369440, 5);

  BrightnessSlider.Color := CTS0(2040359);
  ZoomSlider.Color := CTS0(2106152);
end;

procedure TRSOptions.SetupAlignment(Mode: ERSClientMode); override;
begin
  inherited;

  Self.Alignment.Left := [@GameTab.X1];
  Self.Alignment.Right := [@GameTab.X2];
  Self.Alignment.Top := [@GameTab.Y1];
  Self.Alignment.Bottom := [@GameTab.Y2];

  BrightnessSlider.Bounds.X1 := Self.X1 + 50;
  BrightnessSlider.Bounds.Y1 := Self.Y1 + 76;
  BrightnessSlider.Bounds.X2 := BrightnessSlider.Bounds.X1 + 110;
  BrightnessSlider.Bounds.Y2 := BrightnessSlider.Bounds.Y1 + 9;

  ZoomSlider.Bounds.X1 := Self.X1 + 57;
  ZoomSlider.Bounds.Y1 := Self.Y1 + 109;
  ZoomSlider.Bounds.X2 := ZoomSlider.Bounds.X1 + 96;
  ZoomSlider.Bounds.Y2 := ZoomSlider.Bounds.Y1 + 9;
end;

function TRSOptions.IsOpen: Boolean;
begin
  Result := (GameTabs.GetCurrentTab = ERSGameTab.OPTIONS);
end;

function TRSOptions.Open: Boolean;
begin
  Result := GameTabs.Open(ERSGameTab.OPTIONS);
end;

function TRSOptions.GetButtons: TRSButtonArray;
begin
  Result := Self.FindButtons([[38,38]]);
end;

function TRSOptions.GetButton(Button: ERSOptionsButton): TRSButton;
var
  Buttons: TRSButtonArray := Self.GetButtons;
begin
  if Length(Buttons) = Length(ERSOptionsButton) then
    Result := Buttons[Button];
end;

function TRSOptions.GetHouseOptionsButtons: TRSButtonArray;
begin
  Result := Self.FindButtons([[21, 21], [103, 38], [164, 30]]);
end;

function TRSOptions.GetHouseOptionsButton(Button: ERSHouseOptionsButton): TRSButton;
var
  Buttons: TRSButtonArray;
begin
  Buttons := Self.GetHouseOptionsButtons;
  if High(Buttons) = Ord(High(ERSHouseOptionsButton)) then
    Result := Buttons[Button];
end;

function TRSOptions.IsHouseOptionsOpen: Boolean;
begin
  Result := Self.GetHouseOptionsButton(ERSHouseOptionsButton.CLOSE).Visible;
end;

function TRSOptions.OpenHouseOptions: Boolean;
begin
  if Self.IsHouseOptionsOpen then
    Exit(True);

  if not Self.Open or not Self.OpenTab(ERSOptionsTab.CONTROLS) then
    Exit(False);

  Self.GetButton(ERSOptionsButton.HOUSE).Click;

  Result := WaitUntil(Self.IsHouseOptionsOpen, SRL.TruncatedGauss(50, 1500), 3000);
end;

function TRSOptions.CloseHouseOptions: Boolean;
begin
  if not Self.IsHouseOptionsOpen then
    Exit(True);

  Self.GetHouseOptionsButton(ERSHouseOptionsButton.CLOSE).Click;

  Result := WaitUntil(not Self.IsHouseOptionsOpen, SRL.TruncatedGauss(50, 1500), 3000);
end;


function TRSOptions.FindSlider(var P: TPoint; Slider: TRSSlider): Boolean;
var
  TPA: TPointArray;
begin
  Result := SRL.FindColors(TPA, Slider.Color, Slider.Bounds.Expand(10)) > 0;
  if Result then
    P := TPA.Mean;
end;

function TRSOptions.GetSliderLevel(Slider: TRSSlider): Int32;
var
  B: TBox;
  P: TPoint;
begin
  Result := -1;
  B := Slider.Bounds;
  if B = BrightnessSlider.Bounds then
  begin
    B.X1 += 7;
    B.X2 -= 7;
  end;

  if Self.Open and
     Self.OpenTab(ERSOptionsTab.DISPLAY) and
     Self.FindSlider(P, Slider) then
    Result := Round((P.X - B.X1) * 100 / (B.Width - 1));
end;

function TRSOptions.SetSliderLevel(Level: Int32; Slider: TRSSlider): Boolean;
var
  CurrentLevel: Int32;
  P: TPoint;
  Destination: TPoint;
begin
  CurrentLevel := Self.GetSliderLevel(Slider);
  if (CurrentLevel = Level) then
    Exit(True);

  if Self.FindSlider(P, Slider) then
  begin
    Destination.X := Slider.Bounds.X1 + Round((Level * (Slider.Bounds.Width - 1) / 100)) + 1;
    Destination.Y := Random(Slider.Bounds.Y1, Slider.Bounds.Y2);

    // Move slider away so we can click on destination
    if Distance(P, Destination) <= 12 then
    begin
      if CurrentLevel+20 < 100 then
        Self.SetSliderLevel(CurrentLevel + Random(20,40), Slider)
      else
        Self.SetSliderLevel(CurrentLevel - Random(20,40), Slider);
    end;

    Mouse.Click(Destination, MOUSE_LEFT);
  end;

  Result := WaitUntil(Self.GetSliderLevel(Slider) = Level, SRL.TruncatedGauss(50, 1500), 3000);
end;


function TRSOptions.GetBrightnessLevel: Int32;
begin
  Result := Self.GetSliderLevel(BrightnessSlider);
end;

function TRSOptions.SetBrightnessLevel(Level: Int32): Boolean;
begin
  Result := Self.SetSliderLevel(Level, BrightnessSlider);
end;

function TRSOptions.GetZoomLevel: Int32;
begin
  Result := Self.GetSliderLevel(ZoomSlider);
end;

function TRSOptions.SetZoomLevel(Level: Int32): Boolean;
begin
  Result := Self.SetSliderLevel(Level, ZoomSlider);
end;



function TRSOptions.GetTabs: TBoxArray;
begin
  Result := Grid(3, 1, 55, 24, [9], [Self.X1, Self.Y1 + 5]);
end;

function TRSOptions.GetCurrentTab: ERSOptionsTab;
var
  Tabs: TBoxArray;
  I: Int32;
begin
  Tabs := Self.GetTabs;

  for I := 0 to High(Tabs) do
  begin
    if SRL.CountColor(4741213, Tabs[I]) < 50 then //finds the one without the bottom white line
      Exit(ERSOptionsTab(I));
  end
end;

function TRSOptions.OpenTab(Tab: ERSOptionsTab): Boolean;
begin
  if not Self.Open or not Self.CloseHouseOptions then
    Exit(False);
  if Self.GetCurrentTab = Tab then
    Exit(True);

  Mouse.Click(Self.GetTabs[Tab], MOUSE_LEFT);

  Result := WaitUntil(Self.GetCurrentTab = Tab, SRL.TruncatedGauss(50, 1500), 3000);
end;

procedure TRSOptions.Draw(Bitmap: TMufasaBitmap); override;
begin
  if not Self.IsOpen then
    Exit;

  inherited;

  if Self.IsHouseOptionsOpen then
    Bitmap.DrawButtons(Self.GetHouseOptionsButtons)
  else
  begin
    Bitmap.DrawBoxes(Self.GetTabs, $00FFFF);
    if Self.GetCurrentTab = ERSOptionsTab.DISPLAY then
    begin
      Bitmap.DrawBox(BrightnessSlider.Bounds, $00FFFF);
      Bitmap.DrawBox(ZoomSlider.Bounds, $00FFFF);
    end;
  end;

  Bitmap.DrawButtons(Self.GetButtons);
end;

var
  Options: TRSOptions;

function TRSGameTabs.GetCurrentTab: ERSGameTab; override;
begin
  Result := inherited;

  if (Result = ERSGameTab.UNKNOWN) and Options.IsHouseOptionsOpen then
    Result := ERSGameTab.OPTIONS;
end;

procedure TRSClient.ClientModeChanged; override;
begin
  inherited;

  Options.SetupAlignment(Self.Mode);
end;

procedure TSRL.Setup; override;
begin
  inherited;

  Options.Setup;
end;

procedure TSRL.Debug(Bitmap: TMufasaBitmap); override;
begin
  inherited;

  Options.Draw(Bitmap);
end;

