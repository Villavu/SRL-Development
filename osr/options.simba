{$DEFINE SRL_OPTIONS_INCLUDED}
{$IFNDEF SRL_OSR}
  {$I SRL/osr.simba}
{$ENDIF}

type
(*
type ERSOptionsTab
~~~~~~~~~~~~~~~~~~
  Enumerator type for each of the options gametab tabs.
*)
  ERSOptionsTab = (
    CONTROLS,
    AUDIO,
    DISPLAY
  );

(*
type ERSOptionsButton
~~~~~~~~~~~~~~~~~~~~~
  Enumerator type for each of the main screen of the options gametab buttons.
*)
  ERSOptionsButton = (
    AID,
    RUN,
    HOUSE,
    BOND
  );

(*
type ERSOptionsButton
~~~~~~~~~~~~~~~~~~~~~
  Enumerator type for each of the house screen of the options gametab buttons.
*)
  ERSHouseOptionsButton = (
    CLOSE,
    VIEWER,
    EXPEL,
    SERVANT,
    LEAVE
  );

  TRSOptions = type TRSInterface;
  
procedure TRSOptions.Setup; override;
begin
  inherited;

  Self.Name := 'Options';
  Self.ButtonColors += CTS1(2369440, 5);
end;

procedure TRSOptions.SetupAlignment(Mode: ERSClientMode); override;
begin
  inherited;

  Self.Alignment.Left := [@GameTab.X1];
  Self.Alignment.Right := [@GameTab.X2];
  Self.Alignment.Top := [@GameTab.Y1];
  Self.Alignment.Bottom := [@GameTab.Y2];
end;

(*
Options.GetBrightnessSliderBounds
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSOptions.GetBrightnessSliderBounds: TBox;

Returns a TBox of the brightness slider.

Example
-------

  Debug(Options.GetBrightnessSliderBounds());
*)
function TRSOptions.GetBrightnessSliderBounds(): TBox;
begin
  Result.X1 := Self.X1 + 57;
  Result.Y1 := Self.Y1 + 72;
  Result.X2 := Result.X1 + 96;
  Result.Y2 := Result.Y1 + 9;
end;

(*
Options.GetZoomSliderBounds
~~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSOptions.GetZoomSliderBounds(): TBox;

Returns a TBox of the zoom slider.

Example
-------

  Debug(Options.GetZoomSliderBounds());
*)
function TRSOptions.GetZoomSliderBounds(): TBox;
begin
  Result.X1 := Self.X1 + 57;
  Result.Y1 := Self.Y1 + 109;
  Result.X2 := Result.X1 + 96;
  Result.Y2 := Result.Y1 + 9;
end;

(*
Options.GetSliderLevel
~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSOptions.GetSliderLevel(SliderBounds: TBox; SliderColor: Integer): Integer;

Gives you the level of the passed in **SliderBounds** which can be obtained with **Options.GetBrightnessSliderBounds()**
or **Options.GetZoomSliderBounds()**.

Example
-------

  WriteLn Options.GetSliderLevel(Options.GetZoomSliderBounds(), 2106152); //2106152 is the color of the zoom slider.
*)
function TRSOptions.GetSliderLevel(SliderBounds: TBox; SliderColor: Integer): Integer;
var
  TPA: TPointArray;
begin
  Result := -1;
  if Self.Open() and Self.OpenTab(ERSOptionsTab.DISPLAY) then
  begin
    if SRL.FindColors(TPA, SliderColor, SliderBounds.Expand(10)) > 0 then
      Result := Round((TPA.Mean.X - SliderBounds.X1) * 100 / (SliderBounds.Width() - 1));
  end;
end;

(*
Options.GetZoomLevel
~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSOptions.GetZoomLevel(): Integer;

Wrapper function to get the zoom level. Uses **Options.GetSliderLevel()** internally.

Example
-------

  WriteLn Options.GetZoomLevel();
*)
function TRSOptions.GetZoomLevel(): Integer;
begin
  Result := Self.GetSliderLevel(Self.GetZoomSliderBounds(), 2106152);
end;

(*
Options.SetZoomLevel
~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSOptions.SetZoomLevel(Level: Integer): Boolean;

This method attempts to set our zoom at whatever **Level** is specified.
**Level** should be between 0 and 100.

Example
-------

  WriteLn Options.SetZoomLevel(50);
*)
function TRSOptions.SetZoomLevel(Level: Integer): Boolean;
var
  CurrentLevel: Integer;
  SliderBounds: TBox;
begin
  CurrentLevel := Self.GetZoomLevel();
  if (CurrentLevel = Level) then
    Exit(True);
    
  // Move slider away so we can click the level
  if (Abs(CurrentLevel - Level) <= 20) then
  begin
    if (CurrentLevel + 40 < 100) then
      Self.SetZoomLevel(CurrentLevel + Random(20, 40))
    else
      Self.SetZoomLevel(CurrentLevel - Random(20, 40));
  end;
  
  SliderBounds := Self.GetZoomSliderBounds();
  Mouse.Click(
    SliderBounds.X1 + Round((Level * (SliderBounds.Width() - 1) / 100)) + 1,
    SliderBounds.Y1 + Random(SliderBounds.Y2 - SliderBounds.Y1),
    MOUSE_LEFT
  );

  Result := WaitUntil(InRange(Self.GetZoomLevel(), Level-1, Level+1), SRL.TruncatedGauss(50, 1500), 3000);
end;

(*
Options.GetBrightnessLevel
~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSOptions.GetBrightnessLevel(): Integer;

Wrapper function to get the brightness level. Uses **Options.GetSliderLevel()** internally.

Example
-------

  WriteLn Options.GetBrightnessLevel();
*)
function TRSOptions.GetBrightnessLevel(): Integer;
begin
  Result := Self.GetSliderLevel(Self.GetBrightnessSliderBounds(), 2040359);
end;

(*
Options.SetMaxBrightness
~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSOptions.SetMaxBrightness(): Boolean;

This method attempts to set our brightness at max.

Example
-------

  WriteLn Options.SetMaxBrightness();
*)
function TRSOptions.SetMaxBrightness(): Boolean;
begin
  if Result := Self.OpenTab(ERSOptionsTab.DISPLAY) then
    with Self.GetBrightnessSliderBounds() do
      Mouse.Click(X2 + Random(3, 5), Random(Y1, Y2), MOUSE_LEFT); 
end;

(*
Options.IsOpen
~~~~~~~~~~~~~~
.. pascal:: function TRSOptions.IsOpen(): Boolean;

Returns true if the options tab is open.
*)
function TRSOptions.IsOpen(): Boolean;
begin
  Result := (GameTabs.GetCurrentTab = ERSGameTab.OPTIONS);
end;

(*
Options.Open
~~~~~~~~~~~~
.. pascal:: function TRSOptions.Open(): Boolean;

Attempts to open the options tab.
*)
function TRSOptions.Open(): Boolean;
begin
  Result := GameTabs.Open(ERSGameTab.OPTIONS);
end;

function TRSOptions.GetButtons(): TRSButtonArray;
begin
  Result := Self.FindButtons([[38,38]]);
end;

function TRSOptions.GetButton(Button: ERSOptionsButton): TRSButton;
var
  Buttons: TRSButtonArray := Self.GetButtons;
begin
  if Length(Buttons) = Length(ERSOptionsButton) then
    Result := Buttons[Button];
end;

function TRSOptions.GetHouseOptionsButtons(): TRSButtonArray;
begin
  Result := Self.FindButtons([[21, 21], [103, 38], [164, 30]]);
end;

function TRSOptions.GetHouseOptionsButton(Button: ERSHouseOptionsButton): TRSButton;
var
  Buttons: TRSButtonArray;
begin
  Buttons := Self.GetHouseOptionsButtons;
  if High(Buttons) = Ord(High(ERSHouseOptionsButton)) then
    Result := Buttons[Button];
end;

(*
Options.IsHouseOptionsOpen
~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSOptions.IsHouseOptionsOpen(): Boolean;

Checks wether the house options are open or not.

Example
-------

  WriteLn Options.IsHouseOptionsOpen();
*)
function TRSOptions.IsHouseOptionsOpen(): Boolean;
begin
  Result := Self.GetHouseOptionsButton(ERSHouseOptionsButton.CLOSE).Visible;
end;

(*
Options.OpenHouseOptions
~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSOptions.OpenHouseOptions(): Boolean;

Attempts to open the house options.
*)
function TRSOptions.OpenHouseOptions(): Boolean;
begin
  if Self.IsHouseOptionsOpen then
    Exit(True);

  if not Self.Open or not Self.OpenTab(ERSOptionsTab.CONTROLS) then
    Exit(False);

  Self.GetButton(ERSOptionsButton.HOUSE).Click;

  Result := WaitUntil(Self.IsHouseOptionsOpen, SRL.TruncatedGauss(50, 1500), 3000);
end;

(*
Options.CloseHouseOptions
~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSOptions.CloseHouseOptions(): Boolean;

Attempts to close the house options if they are open.
*)
function TRSOptions.CloseHouseOptions(): Boolean;
begin
  if not Self.IsHouseOptionsOpen then
    Exit(True);

  Self.GetHouseOptionsButton(ERSHouseOptionsButton.CLOSE).Click;

  Result := WaitUntil(not Self.IsHouseOptionsOpen, SRL.TruncatedGauss(50, 1500), 3000);
end;

(*
Options.GetTabs
~~~~~~~~~~~~~~~
.. pascal:: function TRSOptions.GetTabs(): TBoxArray;

Returns a TBoxArray of the tabs in the options gametab.

Example
-------

  Debug(Options.GetTabs());
*)
function TRSOptions.GetTabs(): TBoxArray;
begin
  Result := Grid(3, 1, 55, 24, [9], [Self.X1, Self.Y1 + 5]);
end;

(*
Options.GetCurrentTab
~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSOptions.GetCurrentTab(): ERSOptionsTab;

Returns the current active tab within the options gametab.

Example
-------

  WriteLn Options.GetCurrentTab();
*)
function TRSOptions.GetCurrentTab(): ERSOptionsTab;
var
  Tabs: TBoxArray;
  I: Int32;
begin
  Tabs := Self.GetTabs;

  for I := 0 to High(Tabs) do
  begin
    if SRL.CountColor(4741213, Tabs[I]) < 50 then //finds the one without the bottom white line
      Exit(ERSOptionsTab(I));
  end
end;

(*
Options.OpenTab
~~~~~~~~~~~~~~~
.. pascal:: function TRSOptions.OpenTab(Tab: ERSOptionsTab): Boolean;

Attempts to open the options **Tab** specified.

Example
-------

  WriteLn Options.OpenTab(ERSOptionsTab.DISPLAY);
*)
function TRSOptions.OpenTab(Tab: ERSOptionsTab): Boolean;
begin
  if not Self.Open or not Self.CloseHouseOptions then
    Exit(False);
  if Self.GetCurrentTab = Tab then
    Exit(True);

  Mouse.Click(Self.GetTabs[Tab], MOUSE_LEFT);

  Result := WaitUntil(Self.GetCurrentTab = Tab, SRL.TruncatedGauss(50, 1500), 3000);
end;

procedure TRSOptions.Draw(Bitmap: TMufasaBitmap); override;
begin
  if not Self.IsOpen then
    Exit;

  inherited;

  if Self.IsHouseOptionsOpen() then
    Bitmap.DrawButtons(Self.GetHouseOptionsButtons)
  else
  begin
    Bitmap.DrawBoxArray(Self.GetTabs(), False, $00FFFF);
    if (Self.GetCurrentTab = ERSOptionsTab.DISPLAY) then
    begin
      Bitmap.DrawBox(Self.GetBrightnessSliderBounds(), $00FFFF);
      Bitmap.DrawBox(Self.GetZoomSliderBounds(), $00FFFF);
    end;
  end;

  Bitmap.DrawButtons(Self.GetButtons());
end;

(*
var Options
~~~~~~~~~~~
  Global Options variable.
*)
var
  Options: TRSOptions;

function TRSGameTabs.GetCurrentTab(): ERSGameTab; override;
begin
  Result := inherited;

  if (Result = ERSGameTab.UNKNOWN) and Options.IsHouseOptionsOpen then
    Result := ERSGameTab.OPTIONS;
end;

procedure TRSClient.ClientModeChanged(); override;
begin
  inherited;

  Options.SetupAlignment(Self.Mode);
end;

procedure TSRL.Setup(); override;
begin
  inherited;

  Options.Setup;
end;

procedure TSRL.Debug(Bitmap: TMufasaBitmap); override;
begin
  inherited;

  Options.Draw(Bitmap);
end;

