{$DEFINE SRL_MAGIC_INCLUDED}
{$IFNDEF SRL_OSR}
  {$I SRL/osr.simba}
{$ENDIF}

type
  ERSSpellBook = (
    STANDARD,
    ANCIENT,
    LUNAR,
    ARCEUUS
  );

  ERSSpell = (
    UNKNOWN,
    
    // Standard
    LUMBRIDGE_HOME_TELEPORT,
    WIND_STRIKE,
    CONFUSE,
    ENCHANT_CROSSBOW_BOLT,
    WATER_STRIKE,
    LVL1_ENCHANT,
    EARTH_STRIKE,

    WEAKEN,
    FIRE_STRIKE,
    BONES_TO_BANANAS,
    WIND_BOLT,
    CURSE,
    BIND,
    LOW_LEVEL_ALCHEMY,

    WATER_BOLT,
    VARROCK_TELEPORT,
    LVL2_ENCHANT,
    EARTH_BOLT,
    LUMBRIDGE_TELEPORT,
    TELEKINETIC_GRAB,
    FIRE_BOLT,

    FALADOR_TELEPORT,
    CRUMBLE_UNDEAD,
    TELEPORT_TO_HOUSE,
    WIND_BLAST,
    SUPERHEAT_ITEM,
    CAMELOT_TELEPORT,
    WATER_BLAST,

    LVL3_ENCHANT,
    IBAN_BLAST,
    SNARE,
    MAGIC_DART,
    ARDOUGNE_TELEPORT,
    EARTH_BLAST,
    HIGH_LEVEL_ALCHEMY,

    CHARGE_WATER_ORB,
    LVL4_ENCHANT,
    WATCHTOWER_TELEPORT,
    FIRE_BLAST,
    CHARGE_EARTH_ORB,
    BONES_TO_PEACHES,
    SARADOMIN_STRIKE,

    CLAWS_OF_GUTHIX,
    FLAMES_OF_ZAMORAK,
    TROLLHEIM_TELEPORT,
    WIND_WAVE,
    CHARGE_FIRE_ORB,
    TELEPORT_TO_APE_ATOLL,
    WATER_WAVE,

    CHARGE_AIR_ORB,
    VULNERABILITY,
    LVL5_ENCHANT,
    TELEPORT_TO_KOUREND,
    EARTH_WAVE,
    ENFEEBLE,
    TELEOTHER_LUMBRIDGE,

    FIRE_WAVE,
    ENTANGLE,
    STUN,
    CHARGE,
    WIND_SURGE,
    TELEOTHER_FALADOR,
    WATER_SURGE,

    TELE_BLOCK,
    STANDARD_TARGET_TELEPORT,
    LVL6_ENCHANT,
    TELEOTHER_CAMELOT,
    EARTH_SURGE,
    LVL7_ENCHANT,
    FIRE_SURGE,
    
    // Ancient
    EDGEVILLE_HOME_TELEPORT,
    SMOKE_RUSH,
    SHADOW_RUSH,
    PADDEWWA_TELEPORT,

    BLOOD_RUSH,
    ICE_RUSH,
    SENNTISTEN_TELEPORT,
    SMOKE_BURST,

    SHADOW_BURST,
    KHARYRLL_TELEPORT,
    BLOOD_BURST,
    ICE_BURST,

    LASSAR_TELEPORT,
    SMOKE_BLITZ,
    SHADOW_BLITZ,
    DAREEYAK_TELEPORT,

    BLOOD_BLITZ,
    ICE_BLITZ,
    CARRALLANGAR_TELEPORT,
    ANCIENT_TARGET_TELEPORT,

    SMOKE_BARRAGE,
    SHADOW_BARRAGE,
    ANNAKARL_TELEPORT,
    BLOOD_BARRAGE,

    ICE_BARRAGE,
    GHORROCK_TELEPORT,
    
    // Lunar
    LUNAR_HOME_TELEPORT,
    BAKE_PIE,
    GEOMANCY,
    CURE_PLANT,
    MONSTER_EXAMINE,

    NPC_CONTACT,
    CURE_OTHER,
    HUMIDIFY,
    MOONCLAN_TELEPORT,
    TELE_GROUP_MOONCLAN,

    CURE_ME,
    OURANIA_TELEPORT,
    HUNTER_KIT,
    WATERBIRTH_TELEPORT,
    TELE_GROUP_WATERBIRTH,

    CURE_GROUP,
    STAT_SPY,
    BARBARIAN_TELEPORT,
    TELE_GROUP_BARBARIAN,
    SPIN_FLAX,

    SUPERGLASS_MAKE,
    TAN_LEATHER,
    KHAZARD_TELEPORT,
    TELE_GROUP_KHAZARD,
    DREAM,

    STRING_JEWELLERY,
    STAT_RESTORE_POT_SHARE,
    MAGIC_IMBUE,
    FERTILE_SOIL,
    BOOST_POTION_SHARE,

    FISHING_GUILD_TELEPORT,
    BOUNTY_TELEPORT,
    TELE_GROUP_FISHING_GUILD,
    PLANK_MAKE,
    CATHERBY_TELEPORT,

    TELE_GROUP_CATHERBY,
    RECHARGE_DRAGONSTONE,
    ICE_PLATEU_TELEPORT,
    TELE_GROUP_ICE_PLATEU,
    ENERGY_TRANSFER,

    HEAL_OTHER,
    VENGEANCE_OTHER,
    VENGEANCE,
    HEAL_GROUP,
    SPELLBOOK_SWAP,
    
    // Arceuus
    ARCEUUS_HOME_TELEPORT,
    REANIMATE_GOBLIN,
    LUMBRIDGE_GRAVEYARD_TELEPORT,
    REANIMATE_MONKEY,

    REANIMATE_IMP,
    REANIMATE_MINOTAUR,
    DRAYNOR_MANOR_TELEPORT,
    REANIMATE_SCORPION,

    REANIMATE_BEAR,
    REANIMATE_UNICORN,
    BATTLEFRONT_TELEPORT,
    REANIMATE_DOG,

    MIND_ALTAR_TELEPORT,
    REANIMATE_CHAOS_DRUID,
    RESPAWN_TELEPORT,
    REANIMATE_GIANT,

    SALVE_GRAVEYARD_TELEPORT,
    REANIMATE_OGRE,
    REANIMATE_ELF,
    REANIMATE_TROLL,

    FENKENSTRAINS_CASTLE_TELEPORT,
    REANIMATE_HORROR,
    REANIMATE_KALPHITE,
    WEST_ARDOUGNE_TELEPORT,

    REANIMATE_DAGANNOTH,
    REANIMATE_BLOODVELD,
    HARMONY_ISLAND_TELEPORT,
    REANIMATE_TZHAAR,

    CEMETERY_TELEPORT,
    REANIMATE_DEMON,
    REANIMATE_AVIANSIE,
    RESURRECT_CROPS,

    BARROWS_TELEPORT,
    REANIMATE_ABYSSAL_CREATURE,
    APE_ATOLL_TELEPORT,
    REANIMATE_DRAGON
  );

const
  RS_STANDARD_SPELL_FIRST = ERSSpell.LUMBRIDGE_HOME_TELEPORT;
  RS_STANDARD_SPELL_LAST  = ERSSpell.FIRE_SURGE;

  RS_ANCIENT_SPELL_FIRST  = ERSSpell.EDGEVILLE_HOME_TELEPORT;
  RS_ANCIENT_SPELL_LAST   = ERSSpell.GHORROCK_TELEPORT;
  
  RS_LUNAR_SPELL_FIRST    = ERSSpell.LUNAR_HOME_TELEPORT;
  RS_LUNAR_SPELL_LAST     = ERSSpell.SPELLBOOK_SWAP;

  RS_ARCEUUS_SPELL_FIRST  = ERSSpell.ARCEUUS_HOME_TELEPORT;
  RS_ARCEUUS_SPELL_LAST   = ERSSpell.REANIMATE_DRAGON;
  
  RS_STANDARD_SPELLS = [RS_STANDARD_SPELL_FIRST..RS_STANDARD_SPELL_LAST];
  RS_ANCIENT_SPELLS  = [RS_ANCIENT_SPELL_FIRST..RS_ANCIENT_SPELL_LAST];
  RS_LUNAR_SPELLS    = [RS_LUNAR_SPELL_FIRST..RS_LUNAR_SPELL_LAST];
  RS_ARCEUUS_SPELLS  = [RS_ARCEUUS_SPELL_FIRST..RS_ARCEUUS_SPELL_LAST];
  
type
  TRSMagic = type TRSInterface;

procedure TRSMagic.Setup; override;
begin
  inherited;
  
  Self.Name := 'Magic';
end;

procedure TRSMagic.SetupAlignment(Mode: ERSClientMode); override;
begin
  inherited;

  Self.Alignment.Left := [@GameTab.X1];
  Self.Alignment.Right := [@GameTab.X2];
  Self.Alignment.Top := [@GameTab.Y1];
  Self.Alignment.Bottom := [@GameTab.Y2];
end;

function TRSMagic.IsOpen: Boolean;
begin
  Result := (GameTabs.GetCurrentTab() = ERSGameTab.MAGIC);
end;

function TRSMagic.Open: Boolean;
begin
  Result := Self.IsOpen() or GameTabs.Open(ERSGameTab.MAGIC);
end;

function TRSMagic.GetSpellBook: ERSSpellBook;
const
  ARCEUUS_COLOR := CTS1(4008752, 10); // Colors of the book on the gametab
  ANCIENT_COLOR := CTS1(8273495, 27);
  LUNAR_COLOR   := CTS1(12761525, 27);
var
  Box: TBox := Gametabs.GetTabBox(ERSGameTab.MAGIC); 
begin
  if SRL.CountColor(ARCEUUS_COLOR, Box) >= 85 then Exit(ERSSpellBook.ARCEUUS);
  if SRL.CountColor(ANCIENT_COLOR, Box) >= 85 then Exit(ERSSpellBook.ANCIENT);
  if SRL.CountColor(LUNAR_COLOR,   Box) >= 85 then Exit(ERSSpellBook.LUNAR);
end;

function TRSMagic.IsSpellBook(SpellBook: ERSSpellBook): Boolean;
begin
  Result := SpellBook = Self.GetSpellBook();
end;

function TRSMagic.GetSpellBoxes(out Spellbook: ERSSpellBook): TBoxArray;
begin 
  Spellbook := Self.GetSpellBook();
  case Spellbook of
    ERSSpellBook.STANDARD: Result := Grid(7, 10, 23, 23, [3, 1],   [Self.X1 + 2, Self.Y1 + 1]);
    ERSSpellBook.ARCEUUS:  Result := Grid(4, 9,  23, 23, [25, 4],  [Self.X1 + 8, Self.Y1 + 1]);
    ERSSpellBook.LUNAR:    Result := Grid(5, 9,  23, 23, [17, 4],  [Self.X1 + 2, Self.Y1 + 1]);
    ERSSpellBook.ANCIENT:  Result := Grid(4, 7,  23, 23, [25, 13], [Self.X1 + 8, Self.Y1 + 1]);
  end;
end;

function TRSMagic.GetSpellBox(Spell: ERSSpell; out Box: TBox): Boolean;
var
  Spellbook: ERSSpellBook;
  Boxes: TBoxArray;
begin
  Boxes := Self.GetSpellBoxes(Spellbook);
  
  case Spellbook of
    ERSSpellBook.STANDARD: 
      begin
        Result := (Spell in RS_STANDARD_SPELLS); 
        if Result then 
          Box := Boxes[Spell - Ord(RS_STANDARD_SPELL_FIRST)];
      end;
    
    ERSSpellBook.ARCEUUS: 
      begin
        Result := (Spell in RS_ARCEUUS_SPELLS); 
        if Result then 
          Box := Boxes[Spell - Ord(RS_ARCEUUS_SPELL_FIRST)];
      end;
      
    ERSSpellBook.LUNAR: 
      begin
        Result := (Spell in RS_LUNAR_SPELLS); 
        if Result then 
          Box := Boxes[Spell - Ord(RS_LUNAR_SPELL_FIRST)];
      end;
      
    ERSSpellBook.ANCIENT: 
      begin
        Result := (Spell in RS_ANCIENT_SPELLS); 
        if Result then 
          Box := Boxes[Spell - Ord(RS_ANCIENT_SPELL_FIRST)];
      end;     
  end;
end;

function TRSMagic.MouseSpell(Spell: ERSSpell; Button: Int32 = MOUSE_MOVE): Boolean;
var
  Box: TBox;
begin
  Result := Self.Open() and Self.GetSpellBox(Spell, Box);

  if Result then
  begin
    Mouse.Move(Box);
    if (Button <> MOUSE_MOVE) then
      Mouse.Click(Button);
  end;
end;

function TRSMagic.CanActivate(Spell: ERSSpell): Boolean;
var
  Box: TBox;
begin
  Result := Self.GetSpellBox(Spell, Box) and Self.Open() and (SRL.PeakBrightness(Box) > 35);
end;

function TRSMagic.IsSpellSelected(Spell: ERSSpell): Boolean; 
var
  Box: TBox;
begin
  Result := Self.GetSpellBox(Spell, Box) and Self.Open() and (SRL.CountColor($FFFFFF, Box) > 10);
end;

function TRSMagic.GetSelectedSpell: ERSSpell;
var
  I: Int32;
  Spellbook: ERSSpellBook;
  Boxes: TBoxArray;
begin
  if not Self.IsOpen() then
    Exit;
    
  Boxes := Self.GetSpellBoxes(Spellbook);
 
  for I := 0 to High(Boxes) do 
    if SRL.CountColor($FFFFFF, Boxes[I]) > 10 then 
    case SpellBook of
      ERSSpellBook.STANDARD: Exit(ERSSpell(RS_STANDARD_SPELL_FIRST + I));
      ERSSpellBook.ARCEUUS:  Exit(ERSSpell(RS_ARCEUUS_SPELL_FIRST + I));
      ERSSpellBook.LUNAR:    Exit(ERSSpell(RS_LUNAR_SPELL_FIRST + I));
      ERSSpellBook.ANCIENT:  Exit(ERSSpell(RS_ANCIENT_SPELL_FIRST + I));
    end;
end;

function TRSMagic.CastSpell(Spell: ERSSpell): Boolean;
var
  Selected: ERSSpell;
begin
  if not Self.Open() then
    Exit(False); 

  Selected := Self.GetSelectedSpell();
  if (Selected := Self.GetSelectedSpell()) = Spell then
    Exit(True);
    
  // Unselect
  if (Selected <> ERSSpell.UNKNOWN) then
    Self.MouseSpell(Selected, MOUSE_LEFT);
 
  Result := Self.MouseSpell(Spell, MOUSE_LEFT) and WaitUntil((Self.IsOpen() and (Self.GetSelectedSpell() = Spell)) or Inventory.IsOpen(), 100, SRL.TruncatedGauss(1000, 2000));
end;

procedure TRSMagic.Draw(Bitmap: TMufasaBitmap); override;
var
  Spellbook: ERSSpellBook;
begin
  if not Self.IsOpen() then
    Exit;

  inherited();

  Bitmap.DrawBoxes(Self.GetSpellBoxes(Spellbook), False, $00FFFF);
end;

var
  Magic: TRSMagic;

procedure TRSClient.ClientModeChanged; override;
begin
  inherited;

  Magic.SetupAlignment(Self.Mode);
end;

procedure TSRL.Setup; override;
begin
  inherited;

  Magic.Setup();
end;

procedure TSRL.Debug(Bitmap: TMufasaBitmap); override;
begin
  inherited;

  Magic.Draw(Bitmap);
end;
