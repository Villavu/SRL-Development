{$DEFINE SRL_TBOXARRAY_INCLUDED}
{$IFNDEF SRL_UTILS}
  {$I SRL/utils.simba}
{$ENDIF}

(*
TBoxArray
=========
TBoxArray related methods
*)

procedure TBoxArray.Append(const Value: TBox);
var top:Int32;
begin
  top := Length(Self);
  SetLength(self, top+1);
  Self[top] := Value;
end;

procedure TBoxArray.Insert(const Value: TBox; Index: Integer);
var
  l: UInt32;
begin
  l := Length(Self);
  if (l <= Index) then
  begin
    Self.Append(Value);
    Exit();
  end;

  SetLength(Self, l + 1);
  MemMove(Self[Index], self[Index + 1], (L - Index) * SizeOf(self[0]));
  Self[Index] := Value;
end;

procedure TBoxArray.Del(const Index: Integer; const Count: Integer = 1);
begin
  Delete(Self, Index, Count);
end;

function TBoxArray.Len(): Integer;
begin
  Result := Length(Self);
end;

procedure TBoxArray.Remove(const Value: TBox; const All: Boolean = False);
var
  i: Int32;
  lst: TIntegerArray;
begin
  if (not All) then
  begin
    i := Self.Find(Value);
    if i >= 0 then Self.Del(i);
    Exit;
  end;
  
  lst := Self.FindAll(Value);
  for i:=High(lst) downto 0 do
    Self.Del(lst[i]);
end;

function TBoxArray.Pop(): TBox;
var
  h: Integer;
begin
  h := High(Self);
  Result := Self[h];
  SetLength(Self, h);
end;

procedure TBoxArray.Extend(Arr:TBoxArray);
var L:Int32;
begin
  if Length(arr) > 0 then
  begin
    L := Length(Self);
    SetLength(Self, Length(Arr) + L);
    MemMove(Arr[0],Self[L],Length(Arr)*SizeOf(TBox));
  end;
end; 

function TBoxArray.Find(const Value: TBox): Integer; constref;
begin
  for Result := 0 to High(Self) do
    if Self[result] = Value then
      Exit;

  Result := -1;
end;

function TBoxArray.FindAll(const Value: TBox): TIntegerArray; constref;
var
  i: UInt32;
begin
  for i:=0 to High(Self) do
    if Self[i] = Value then
      Result += i;
end;

function TBoxArray.Reversed(): TBoxArray;
var
  lo:PtrUInt;
  p,r:^TBox;
begin
  if (Length(Self) = 0) then
    Exit();

  SetLength(Result,length(self));
  p := @Self[high(self)];
  r := @Result[0];

  lo := PtrUInt(@Self[0]);
  while (lo<=PtrUInt(p)) do
  begin
    r^ := p^;
    dec(p);
    inc(r);
  end;
end;

procedure TBoxArray.Reverse();
var
  tmp:TBox;
  lo,hi:^TBox;
begin
  if (Length(Self) = 0) then
    Exit();

  lo := @Self[0];
  hi := @Self[High(self)];
  while (PtrUInt(lo)<PtrUInt(hi)) do
  begin
    tmp := hi^;
    hi^ := lo^;
    lo^ := tmp;
    dec(hi);
    inc(lo);
  end;
end;

function TBoxArray.Combine(Other: TBoxArray): TBoxArray; constref;
begin
  SetLength(Result, Length(Self) + Length(Other));
  if Length(self) > 0 then
    MemMove(self[0], result[0], Length(self)*SizeOf(self[0]));
  if Length(Other) > 0 then
    MemMove(Other[0], result[Length(Self)], Length(Other)*SizeOf(Other[0]));
end;

function TBoxArray.Equals(Other:TBoxArray): Boolean; constref;
begin
  if (Length(Self) <> Length(Other)) then Exit(False);
  if (Length(Self) = 0) then Exit(True);
  Result := CompareMem(Self[0], Other[0], length(self)*SizeOf(self[0]));     
end;

//--| Type helpers (extended) |------------------------------------------------>
//----------------------------------------------------------------------------->

procedure TBoxArray.Sort(weights: TIntegerArray; iLo, iHi: Int32; SortUp: Boolean);
var
  lo, hi, mid: Int32;
begin
  if (Length(weights) <> Length(self)) then Exit;
  lo := iLo;
  hi := iHi;
  Mid := weights[(lo + hi) shr 1];
  repeat
    if SortUp then
    begin
      while (weights[lo] < mid) do Inc(lo);
      while (weights[hi] > mid) do Dec(hi);
    end else
    begin
      while (weights[Lo] > mid) do Inc(Lo);
      while (weights[Hi] < mid) do Dec(Hi);
    end;
    if (lo <= hi) then
    begin
      Swap(weights[lo], weights[hi]);
      Swap(self[lo], self[hi]);
      Inc(lo);
      Dec(hi);
    end;
  until Lo > Hi;
  if (hi > iLo) then self.Sort(weights, iLo, hi, SortUp);
  if (lo < iHi) then self.Sort(weights, lo, iHi, SortUp);
end;

procedure TBoxArray.SortFromMidPoint(From: TPoint);
var
  I, L: Integer;
  Mid: TPoint;
  weights: TIntegerArray;
begin
  L := High(Self);
  if (L <= 0) then
    Exit;

  SetLength(weights, L + 1);
  for i := 0 To L do
  begin
    Mid := MiddleBox(Self[i]);
    weights[i] := Round(Sqr(From.X - Mid.X) + Sqr(From.Y - Mid.Y));
  end;

  Self.Sort(weights, 0, L, True);
end;

procedure TBoxArray.ModifySide(side:String='x1'; change:Int32=1);
var i:Int32;
begin
  side := LowerCase(side);
  if Length(side) < 2 then Exit();
  SetLength(side, 2);
  for i:=0 to High(self) do
    case Side of
      'x1': self[i].x1 += change;
      'y1': self[i].y1 += change;
      'x2': self[i].x2 += change;
      'y2': self[i].y2 += change;
    end;
end;

function TBoxArray.Offset(pt:TPoint): TBoxArray;
var i:Int32;
begin
  SetLength(Result, Length(Self));
  
  for i:=0 to High(self) do
  begin
    Result[i].X1 := Self[I].X1 + pt.x;
    Result[i].Y1 := Self[I].Y1 + pt.y;
    Result[i].X2 := Self[I].X2 + pt.x;
    Result[i].Y2 := Self[I].Y2 + pt.y;
  end;
end;

function TBoxArray.Merge(): TBox; constref;
var i:Int32;
begin
  if Length(Self) = 0 then
    Exit;

  Result := Self[0];
  for i:=1 to High(self) do
    Result := Result + Self[i];
end;

procedure TBoxArray.SortByX(LowToHi: Boolean = True);
var
  Weights: TIntegerArray;
  I: Int32;
begin
  if Length(Self) = 0 then
    Exit;

  SetLength(Weights, Length(Self));
  for I := 0 to High(Weights) do
    Weights[I] := Self[I].X1;

  Sort(Weights, 0, High(Self), LowToHi);
end;

procedure TBoxArray.SortByY(LowToHi: Boolean = True);
var
  Weights: TIntegerArray;
  I: Int32;
begin
  if Length(Self) = 0 then
    Exit;

  SetLength(Weights, Length(Self));
  for I := 0 to High(Weights) do
    Weights[I] := Self[I].Y1;

  Sort(Weights, 0, High(Self), LowToHi);
end;

// Sorts column wise
// Example: [0][1][2][3]
//          [4][5][6]
procedure TBoxArray.SortByXY(Tolerance: Int32 = 0);
var
  I, J: Int32;
  Weights: TIntegerArray;
  Means: TPointArray;
begin
  if Length(Self) = 0 then
    Exit;
  
  SetLength(Weights, Length(Self)); 
  SetLength(Means, Length(Self));
  for I := 0 to High(Self) do
    Means[I] := Self[I].Middle();

  for I := 0 to High(Self) do
  begin
    Weights[I] := Means[I].X + (Means[I].Y * 1000);
    if Tolerance = 0 then
      Continue;
      
    for J := 0 to High(Self) do
      if Abs(Means[I].Y - Means[J].Y) < Tolerance then
        Weights[I] := Means[I].X + (Means[J].Y * 1000);
  end;
    
  Self.Sort(Weights, 0, High(Weights), True);
end;

function TBoxArray.Middles: TPointArray; constref;
var
  I: Int32;
begin
  SetLength(Result, Length(Self));
  for I := 0 to High(Self) do
    Result[I] := Self[I].Middle();
end;

function Grid(Columns, Rows, Width, Height: Int32; Spacing, Start: TPoint): TBoxArray;
var
  I: Int32;
begin
  Start.X += (Width div 2);
  Start.Y += (Height div 2);

  Spacing.X += Width;
  Spacing.Y += Height;

  SetLength(Result, Columns * Rows);

  for I := 0 to High(Result) do
  begin
    Result[I].X1 := Start.X + I mod Columns * Spacing.X - Width div 2;
    Result[I].Y1 := Start.Y + I div Columns * Spacing.Y - Height div 2;
    Result[I].X2 := Result[I].X1 + Width;
    Result[I].Y2 := Result[I].Y1 + Height;
  end;
end;

function TBoxArray.Pack: TBoxArray;
type
  TBlock = record X,Y,W,H: Integer; Index: Integer; end;
  TBlockArray = array of TBlock;

  function Block(X,Y,W,H: Integer; Index: Integer = -1): TBlock;
  begin
    Result.X := X;
    Result.Y := Y;
    Result.W := W;
    Result.H := H;
    Result.Index := Index;
  end;
var
  weights: TIntegerArray;
  I, J: Integer;
  StartWidth, Area, MaxWidth: Integer;
  Width, Height: Integer;
  Blocks, Spaces: TBlockArray;
  Len: Integer;
begin
  Len := Length(Self);
  if (Len = 0) then
    Exit(Default(TBoxArray));

  Area := 0;
  MaxWidth := 0;

  SetLength(Result, Len);
  SetLength(Blocks, Len);
  SetLength(Weights, Len);

  for I := 0 to Len - 1 do
  begin
    Blocks[I] := Block(0, 0, Self[I].Width - 1, Self[I].Height - 1, I);
    Weights[I] := Blocks[I].H;

    Area += Blocks[I].W * Blocks[I].H;
    MaxWidth := Max(MaxWidth, Blocks[I].W);
  end;

  Blocks := Sorted(Blocks, Weights, False);

  StartWidth := Max(Ceil(Sqrt(Area / 0.95)), MaxWidth);
  Spaces := [Block(0, 0, StartWidth, $FFFFFF)];

  Width := 0;
  Height := 0;

  for I := 0 to Len - 1 do
    for J := High(Spaces) downto 0 do
    begin
      if (Blocks[I].W > Spaces[J].W) or (Blocks[I].H > Spaces[J].H) then
        Continue;

      Blocks[I].X := Spaces[J].X;
      Blocks[I].Y := Spaces[J].Y;

      Width  := Max(Width, Blocks[I].X + Blocks[I].W);
      Height := Max(Height, Blocks[I].Y + Blocks[I].H);

      if (Blocks[I].W = Spaces[J].W) and (Blocks[I].H = Spaces[J].H) then
        Delete(Spaces, J, 1)
      else
      if (Blocks[I].H = Spaces[J].H) then
      begin
        Spaces[J].X += Blocks[I].W;
        Spaces[J].W -= Blocks[I].W;
      end else
      if (Blocks[I].W = Spaces[J].W) then
      begin
        Spaces[J].Y += Blocks[I].H;
        Spaces[J].H -= Blocks[I].H;
      end else
      begin
        Spaces += [Block(
          Spaces[J].X + Blocks[I].W,
          Spaces[J].Y,
          Spaces[J].W - Blocks[I].W,
          Blocks[I].H
        )];
        Spaces[J].Y += Blocks[I].H;
        Spaces[J].H -= Blocks[I].H;
      end;

      Break;
    end;

  for I := 0 to Len - 1 do
    Result[Blocks[I].Index] := Box(Blocks[I].X, Blocks[I].Y, Blocks[I].X + Blocks[I].W, Blocks[I].Y + Blocks[I].H);
end;

